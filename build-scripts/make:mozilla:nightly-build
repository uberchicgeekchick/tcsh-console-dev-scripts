#!/bin/tcsh -f
set mozilla_dir="/programs/Mozilla";
set tarball_backup_dir="/uberChick/Downloads/Mozilla/Nightly-Builds";

if ( "${?1}" == "0" || "${1}" == "" || "${1}" == "--help" ) then
	printf "Usage: %s (firefox|thunderbird)\n\tchecks out the latest source code for the mozilla project, builds it, makes its package(tarball), and back-ups the built tarball using the build day.\n" `basename "${0}"`;
	exit -1;
endif

cd "${mozilla_dir}";

if ( "${?GREP_OPTIONS}" == "1" ) unsetenv GREP_OPTIONS;

if ( ! -d "src" ) mkdir "src";
cd src;

switch ( "${1}" )
case "firefox":
	set build_mozilla="firefox";
	set mercurial_repo="mozilla-central";
	breaksw
case "thunderbird":
default:
	set build_mozilla="thunderbird";
	set mercurial_repo="comm-central";
	breaksw
endsw

if ( ! -d "${build_mozilla}-src/.hg" ) hg clone "http://hg.mozilla.org/${mercurial_repo}/" "${build_mozilla}-src";

cd "${build_mozilla}-src";
hg up;

if ( ! -e ".mozconfig" ) then
	switch ( "${build_mozilla}" )
	case "firefox":
		set enable_mozilla="--enable-application=browser";
		breaksw
	case "thunderbird":
	default:
		set enable_mozilla="--enable-application=mail --enable=calendar";
		breaksw
	endsw
	
	printf "ac_add_options ${enable_mozilla} --enable-installer --disable-pedantic --enable-static --disable-shared --enable-optimize --enable-default-toolkit=cairo-gtk2 --disable-tests --disable-mochitest\nmk_add_options MOZ_OBJDIR=@TOPSRCDIR@/../${build_mozilla}-objdir\nmk_add_options AUTOCONF=autoconf-2.13\n" > ".mozconfig";
endif

python client.py checkout

make -f client.mk build

cd "../${build_mozilla}-objdir"

make package

set today=`date '+%Y-%m-%d'`
set todays_tarball="`basename mozilla/dist/${build_mozilla}-*.tar.bz2 | sed 's/\(thunderbird.*\)\(\.tar\.bz2\)/\1\.${today}\2/g'`"

cp mozilla/dist/${build_mozilla}-*.tar.bz2 "${tarball_backup_dir}/${todays_tarball}"

cd ..

