#!/bin/tcsh -f
while( ${?1} && "${1}" != "" )
	switch("${1}")
		case "--firefox":
		case "--links":
		case "--lynx":
			set browser="`echo -n '${1}' | sed -r 's/[\-]{2}(.*)/\1/'`";
			shift;
			breaksw
		default:
			if( ! ${?browser} && "`echo -n '${1}' | sed -r 's/[\-]{2}(browser)=(.*)/\1/'`" == "browser" ) then
				set exec_test="`echo -n '${1}' | sed -r 's/[\-]{2}browser=(.*)/\1/'`";
				foreach browser("`where '${exec_test}'`")
					if( ${?exec_test} ) unset exec_test;
					if( -x "${browser}" ) then
						shift;
						breaksw
					endif
				end
				if( ${?browser} ) unset browser;
			endif
			
			if( ${?url} ) then
				set url="${url} ${1}";
			else
				set url="${1}";
			endif
			shift;
			breaksw
	endsw
end

if(! ${?browser} ) then
	if( "${TERM}" == "gnome" ) then
		set browser="firefox";
	else
		set browser="links"
	endif
endif

if( -x "${browser}" ) then
	set program="${browser}";
else
	foreach program ( "`which '${browser}'`" )
		if( "${program}" != "${0}" && -x "${program}" ) break;
		unset program;
	end
endif

if(! ${?program} ) goto noexec;
if(! -x "${program}" ) goto noexec;
goto launchers_main;

noexec:
	printf "Unable to find %s.\n" "${browser}";
	if( ${?program} ) unset program;
	unset browser;
	exit -1;

launchers_main:

	if(! ${?url} )	\
		set url="http://slashdot.org/"
	
	${program} "${url}";

