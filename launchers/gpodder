#!/bin/tcsh -f
if( "`echo '${0}' | sed -r 's/^[^\.]*(tcsh)/\1/'`" == "tcsh" ) exit;

set gPodders_path="`dirname ${0}`";
set gPodder_script="gpodder-0.11.3-hacked";
if ( ! -x "${gPodders_path}/${gPodder_script}" ) then
	printf "Unable to find and run `%s/%s`" "${gPodders_path}" "${gPodder_script}";
	exit -1;
endif

if( "`alias cwdcmd`" != "" ) then
	set oldcwdcmd="`alias cwdcmd`";
	unalias cwdcmd;
endif

if(! ${?eol} ) setenv eol '$';

set argz="";
next_argv:
while( ${?1} && "${1}" != "" )
	set message="";
	set action="`printf "\""${1}"\"" | sed 's/\-\-\([^=]\+\)=\?\(.*\)${eol}/\1/'`";
	set feed="`printf "\""${1}"\"" | sed 's/\-\-\([^=]\+\)=\?\(.*\)${eol}/\2/'`";
	switch( "${action}" )
	case "debug":
		set tcshrc_debug_set;
		set TCSH_RC_DEBUG;
		shift;
		continue;
		breaksw;
	case "add":
		if( "${message}" == "" ) set message="add";
	case "del":
		if( "${message}" == "" ) set message="delete";
		set argz=--${action}="${feed}";
		breaksw;
	default:
		set argz=${1};
		breaksw;
	endsw
	shift;
	goto run_progie;
end

if(! ${?gpodder_ran} ) then
	set gpodder_run_once message feed argz;
	goto run_progie;
endif

if( ${?gpodder_run_once} ) unset gpodder_run_once;
if( ${?tcshrc_debug_set} ) then
	unset tcshrc_debug_set;
	unsetenv TCSH_RC_DEBUG;
endif

cd "${owd}";

if( ${?oldcwdcmd} ) then
	alias cwdcmd "${oldcwdcmd}";
	unset oldcwdcmd;
endif
unset gPodders_path gPodder_script gpodder_ran message feed argz;

exit;

run_progie:
if(! ${?gpodder_ran} ) set gpodder_ran;
if( "${message}" != "" && "${feed}" != "" ) printf "Attempting to %s:\n\t<%s>\n" "${message}" "${feed}";
if( ${?TCSH_RC_DEBUG} ) echo "Running:\n\t./${gPodder_script} --local ${argz}";
cd "${gPodders_path}";
if( ! ${?OUTPUT_STDERR} ) then
	./${gPodder_script} --local "${argz}";
else
	./${gPodder_script} --local "${argz}" >& /dev/null;
endif
cd "${owd}";
if(! ${?gpodder_run_once} ) goto next_argv;

