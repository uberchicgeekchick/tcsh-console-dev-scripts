#!/bin/tcsh -f

if(!( ${?1} && "${1}" != "" && ( ( -e "${1}" || "${1}" == "--transcode" ) && ( "${2}" == "" || -e "${2}" ) ) )) then
	printf "Usage: %s [--rm-orginal] input_file" `basename "${0}"`;
	exit -1;
endif

set decoder=`which ffmpeg`;
if(! -x "${decoder}" ) then
	printf "Unable to find executable decoder: ffmpeg.  Found: %s.\n" ${decoder};
	exit -2;
endif

set encoder=`which oggenc`;
if(! -x "${encoder}" ) then
	printf "Unable to find executable encoder: oggenc.  Found: %s.\n" ${decoder};
	exit -3;
endif

if( "${1}" == "--transcode" ) then
	set transcode;
	shift;
endif

set filename=`echo ${1} | sed 's/\(.*\)\.\([^\.]\+\)$/\1/g'`;
set extension=`echo ${1} | sed 's/\(.*\)\.\([^\.]\+\)$/\2/g'`;
if( -e "${filename}.ogg" ) then
	printf "%s.ogg already exists... exiting."
	exit(-4);
endif

if( "${extension}" != "wav" ) then
	if( -e "${filename}.wav" ) rm "${filename}.wav";
	printf "Please wait a moment...\n\tTemporally converting\n\t\t%s\n\t to WAV file:" "${1}"
	${decoder} -i "${1}" -ac 1 -ab 64000 "${filename}.wav" >& /dev/null;
	if(!("${status}" == "0" && -e "${filename}.wav")) then
		printf "\t\t[failed]\nUnable to decode file.  Decoder: %s; returned: %d.\n" `basename ${decoder}` ${status};
		exit ${status};
	endif
	printf "\t\t[success]\n"
	if( ${?transcode} ) rm "${1}";
endif

printf "Please wait a moment...\n\tEncoding WAV\n\t\t%s.wav\n\t to OGG Vorbis:" "${filename}"

if( -e "${filename}.ogg" ) rm "${filename}.ogg";
${encoder} "${filename}.wav" >& /dev/null;
if(!("${status}" == "0" && -e "${filename}.ogg")) then
	printf "\t\t[failed]\nUnable to ecode file.  Encoder: %s; returned: %d.\n" `basename ${decoder}` ${status};
	exit ${status}
endif

printf "\t\t[success]\n\tCreated OGG Vorbis:\n\t\t%s.ogg\n" "${filename}"
rm "${filename}.wav"
