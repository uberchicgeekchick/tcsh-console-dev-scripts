#!/bin/tcsh -f
set server="192.168.4.8";
set shares=( "projects/games" "projects/gtk" "projects/art" "projects/cli" "projects/media" "projects/www" "projects/reference" "media/podcasts" "media/podiobooks" "media/library" "media/tv-shows" "media/movies" "uberChick" "programs" "profile.d" "srv" );

next_mount:
if( ${?action} ) unset action;
while( ${?1} && "${1}" != "" )
	switch( ${1} )
	case "--via-lan":
		set server="192.168.4.8";
		breaksw
	case "--via-wifi":
		set server="192.168.4.4";
		breaksw
	case "--mount":
		set action="mount";
		set to_or_from="to";
		breaksw
	case "--umount":
		set action="umount";
		set to_or_from="from";
		breaksw
	case "--help":
		goto usage;
		breaksw
	default:
		set server="`echo '${1}' | sed -r 's/\-\-(server)\=([0-9\.]+)/\1/'`";
		if( "${server}" != "server" ) then
			unset server;
		else
			set server="`echo '${1}' | sed -r 's/\-\-(server)\=([0-9\.]+)/\2/'`";
			breaksw
		endif
		if(! ${?action} ) then
			if(! ${?help_displayed} ) goto usage
		endif
		set single_mount="${1}";
		breaksw
	endsw
	shift;
end

if(! ${?action} ) then
	if(! ${?help_displayed} ) goto usage
endif

if( "${uid}" != "0" ) set action="sudo ${action}";
foreach share ( ${shares} )
	if( ${?single_mount} ) then
		if( "${single_mount}" != "/${share}" ) continue;
	endif
	printf "%sing %s:/%s %s /%s/nfs\n" "${action}" "${server}" "${share}" "${to_or_from}" "${share}";
	if( "${action}" == "umount" ||  "${action}" == "sudo umount" ) then
		${action} -f "/${share}/nfs";
	else
		if( ! -d "/${share}/nfs" ) mkdir -p "/${share}/nfs";
		${action} -t nfs4 -o defaults,users "${server}:/${share}" "/${share}/nfs";
	endif
	if( ${?single_mount} ) then
		unset single_mount;
		goto next_mount
	endif
end

if( ${?script} ) unset script;
if( ${?action} ) unset action;
unset server shares share  to_or_from
exit;

usage:
set help_displayed;
set script="`basename '${0}'`";
printf "%s --mount|umount [nfs4_share...]\n" "${script}";
printf "\tBy default %s mounts all nfs4 shares.\n\t[nfs4_share] is optional.\n\tIf specified only those shares are mouted, or unmouted.\n\tAvailable shares are:\n" "${script}";
foreach share ( ${shares} )
	printf "\t\t%s:/%s on %s\n" "${server}" "${share}";
end
if(! ${?action} ) exit -1;
goto next_mount

