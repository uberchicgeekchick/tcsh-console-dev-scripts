#!/bin/tcsh -f
if(! ${?TCSH_RC_SOURCE_FILE} )	\
	setenv TCSH_RC_SOURCE_FILE "$argv[1]";

if( ! ${?TCSH_OUTPUT_ENABLED} && ! ${?TCSH_OUTPUT_DISABLED} ) then
	#if( ${?TTY} && ! ${?SSH_CONNECTION} ) then
	if( ! ${?SSH_CONNECTION} ) then
		setenv TCSH_OUTPUT_ENABLED;
	else
		setenv TCSH_OUTPUT_DISABLED;
	endif
endif

@ args_handled=0;
@ argc=${#argv};
@ arg=0;
if(! ${?TCSH_RC_DEBUG} ) then
	while( $arg < $argc )
		@ arg++;
		switch( "$argv[$arg]" )
			case "--debug":
				@ args_handled++;
				@ arg=${#argv};
				if(! ${?TCSH_OUTPUT_ENABLED} ) breaksw;
				if( ${?TCSH_RC_DEBUG} ) then
					if( "${TCSH_RC_DEBUG}" != "$argv[1]" ) breaksw;
				endif
				
				set argv[$arg]="";
				printf "[%s]: enabling verbose debugging output @ %s.\n" "$argv[1]" `date "+%I:%M:%S%P"`;
				setenv TCSH_RC_DEBUG "$argv[1]";
				break;
			breaksw;
		endsw
	end
	if( ! ${?TCSH_RC_DEBUG} || $arg > 1 )	\
		@ arg=0;
endif

if( ${?TCSH_RC_DEBUG} ) then
	printf "Checking %s's argv options.  %d total.\n" "$argv[1]" "${argc}";
else if( "`alias cwdcmd`" != "" ) then
	if( ${?TCSH_RC_DEBUG} ) printf "Saving cwdcmd until TCSH finishes loading.\n";
	setenv oldcwdcmd "`alias cwdcmd`";
	unalias cwdcmd;
endif

while( $arg < $argc )
	@ arg++;
	if( ${?TCSH_RC_DEBUG} ) printf "Checking %sargv[%sarg], %d of %d's value: %s\n" '$' '$' "${arg}" "${argc}" "$argv[$arg]";
	
	set dashes="`printf "\""$argv[$arg]"\"" | sed -r 's/([\-]{1,2})([^\=]+)=?['\''"\""]?(.*)['\''"\""]?/\1/'`";
	if( "${dashes}" == "$argv[$arg]" ) set dashes="";
	
	set option="`printf "\""$argv[$arg]"\"" | sed -r 's/([\-]{1,2})([^\=]+)=?['\''"\""]?(.*)['\''"\""]?/\2/'`";
	if( "${option}" == "$argv[$arg]" ) set option="";
	
	set value="`printf "\""$argv[$arg]"\"" | sed -r 's/([\-]{1,2})([^\=]+)=?['\''"\""]?(.*)['\''"\""]?/\3/'`";
	if( "${value}" == "$argv[$arg]" ) set value="";
	
	switch( "${option}" )
		case "cd":
		case "root":
		case "use-root":
		case "working-dir":
		case "working-directory":
			@ args_handled++;
			if(!( "${value}" != "" && -d "${value}" )) then
				@ arg++;
				if( $arg > $argc ) then
					if( ${?TCSH_OUTPUT_ENABLED} ) then
						printf "Using [%s] as TCSH's working directory.\t" "${value}";
						printf "[failed]\n\tNo valid directory was specified with %s.\n" "${option}";
					endif
					continue;
				endif
				@ args_handled++;
				set value="$argv[$arg]";
				if(!( "${value}" != "" && -d "${value}" )) then
					if( ${?TCSH_OUTPUT_ENABLED} ) then
						printf "Using [%s] as TCSH's working directory.\t" "${value}";
						printf "[failed]\n\tNo valid directory was specified with %s.\n" "${option}";
					endif
					continue;
				endif
			endif
			if( ${?TCSH_OUTPUT_ENABLED} ) printf "Using [%s] as TCSH's working directory.\t" "${value}";
			if( ${?skip_cwd_test} ) continue;
			setenv TCSH_RC_WORKING_DIR "$value";
			if( ${?TCSH_OUTPUT_ENABLED} ) printf "[success]\n";
			if( "${cwd}" != "${value}" ) cd "${value}";
		breaksw
	endsw
end
if( ${?TCSH_RC_DEBUG} ) printf "Started loading %s @ %s.\n\n" $argv[1] `date "+%I:%M:%S%P"`;
exit $args_handled;
