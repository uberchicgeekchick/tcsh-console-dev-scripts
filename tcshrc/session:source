#!/bin/tcsh -f

source /projects/cli/tcshrc/debug:check session:source ${argv};

if( ! ${?TCSH_SESSION_RC_PATH} ) setenv TCSH_SESSION_RC_PATH "/projects/cli/tcshrc";

source "${TCSH_SESSION_RC_PATH}/paths.tcsh";

if( ${uid} != 0 ) then
	if( -e "${TCSH_SESSION_RC_PATH}/aliases:sudo:services.tcsh" ) then
		if( ${?TCSHRC_DEBUG} ) printf "Setting up sudo services aliases @ %s.\n" `date "+%I:%M:%S%P"`;
		source "${TCSH_SESSION_RC_PATH}/aliases:sudo:services.tcsh";
		if( ${?TCSHRC_DEBUG} ) printf "sudo services aliases setup completed @ %s.\n" `date "+%I:%M:%S%P"`;
	endif
	
	if( -e "${TCSH_SESSION_RC_PATH}/aliases:sudo:commands.tcsh" ) then
		if( ${?TCSHRC_DEBUG} ) printf "Setting up sudo command aliases @ %s.\n" `date "+%I:%M:%S%P"`;
		source "${TCSH_SESSION_RC_PATH}/aliases:sudo:commands.tcsh";
		if( ${?TCSHRC_DEBUG} ) printf "sudo command aliases setup completed @ %s.\n" `date "+%I:%M:%S%P"`;
	endif
endif

foreach rc_file ( "${TCSH_SESSION_RC_PATH}/"*.tcsh )
	set rc_file="`basename '${rc_file}'`";
	
	switch("${rc_file}")
	case "bindkey.tcsh":
	case "complete.tcsh":
	case "environment.tcsh":
	case "history.tcsh":
	case "paths.tcsh":
	case "art:paths.tcsh":
	case "paths:lib.tcsh":
	case "paths:lib64.tcsh":
	case "paths:programs.tcsh":
	case "aliases:sudo:services.tcsh":
	case "aliases:sudo:commands.tcsh":
	case "art.tcsh":
	case "art:alacast.tcsh":
	case "art:editor.tcsh":
	case "art:devel:make:init.tcsh":
		if( ${?TCSHRC_DEBUG} ) printf "Skipping %s @ %s.\n" "${rc_file}" `date "+%I:%M:%S%P"`;
		breaksw;
	default:
		if( ${?TCSHRC_DEBUG} ) printf "Sourcing: %s @ %s\n" "${rc_file}" `date "+%I:%M:%S%P"`;
		source "${TCSH_SESSION_RC_PATH}/${rc_file}";
		if( ${?TCSHRC_DEBUG} ) printf "%s finished loading %s\n" "${rc_file}" `date "+%I:%M:%S%P"`;
		if( ${?http_proxy} ) unsetenv http_proxy;
		breaksw;
	endsw
end

if( ${?TCSHRC_DEBUG} ) printf "Setting up artistic programming canvas @ %s.\n" `date "+%I:%M:%S%P"`;
source "${TCSH_SESSION_RC_PATH}/art.tcsh";
if( ${?TCSHRC_DEBUG} ) printf "Setting up make & gcc environment @ %s.\n" `date "+%I:%M:%S%P"`;
source "${TCSH_SESSION_RC_PATH}/art:devel:make:init.tcsh";
if( ${?TCSHRC_DEBUG} ) printf "Setting up EDITOR as VIM & advanced bindings @ %s.\n" `date "+%I:%M:%S%P"`;
source "${TCSH_SESSION_RC_PATH}/art:editor.tcsh";

if( ${?TCSHRC_DEBUG} ) printf "\nSession set up completed @ %s\n" `date "+%I:%M:%S%P"`;

unset rc_file;

source /projects/cli/tcshrc/debug:clean-up session:source;
